# Licensed under the MIT License
# https://github.com/craigahobbs/markdown-up/blob/main/LICENSE

include <args.bare>


#
# The MarkdownUp file browser application main entry point
#
async function markdownUpIndex():
    # Parse arguments
    arguments = argsValidate([{'name': 'path'}])
    args = argsParse(arguments)
    path = objectGet(args, 'path')

    # Fetch the index files/directories/parent API
    index_url = 'markdown_up_index' + if(path != null, '?path=' + urlEncodeComponent(path), '')
    index = jsonParse(systemFetch(index_url))

    # Set the document title
    title = 'MarkdownUp - ' + objectGet(index, 'path')
    documentSetTitle(title)

    # Menu and title
    parent = objectGet(index, 'parent')
    markdownPrint( \
        if(path == null, 'Root', argsLink(arguments, 'Root', {'path': null})) + ' |', \
        if(path == null, 'Parent', if(parent == null, \
            argsLink(arguments, 'Parent', {'path': null}), \
            argsLink(arguments, 'Parent', {'path': parent}))) + ' |', \
        '[MarkdownUp](https://github.com/craigahobbs/markdown-up#readme)', \
        '', \
        '# ' + markdownEscape(title) \
    )

    # Render file links
    files = objectGet(index, 'files')
    if files:
        markdownPrint('', '## Files')
        for file in arraySort(files, markdownUpIndexFileCompare):
            fileName = objectGet(file, 'name')
            fileDisplay = objectGet(file, 'display', fileName)
            fileURL = if(path != null, path + '/', '') + fileName
            markdownPrint('', '[' + markdownEscape(fileDisplay) + '](' + urlEncode(fileURL) + ')')
        endfor
    endif

    # Render directory links
    directories = objectGet(index, 'directories')
    if directories:
        markdownPrint('', '## Directories')
        for directory in arraySort(directories):
            directoryURL = if(path != null, path + '/', '') + directory
            markdownPrint('', argsLink(arguments, directory, {'path': directoryURL}))
        endfor
    endif

    # Empty path?
    if !files && !directories:
        markdownPrint('', 'No files or sub-directories found')
    endif
endfunction


function markdownUpIndexFileCompare(file1, file2):
    name1 = objectGet(file1, 'display', objectGet(file1, 'name'))
    name2 = objectGet(file2, 'display', objectGet(file2, 'name'))
    return systemCompare(name1, name2)
endfunction
